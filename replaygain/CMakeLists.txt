if (FFMPEG_FOUND OR MPG123_FOUND)
    if (FFMPEG_FOUND)
        set(CANTATA_RG_SRCS ${CANTATA_RG_SRCS} ffmpeginput.cpp)
    endif ()
    if (MPG123_FOUND)
        set(CANTATA_RG_SRCS ${CANTATA_RG_SRCS} mpg123input.cpp)
        include_directories(${MPG123_INCLUDE_DIRS})
    endif ()

    set(CANTATA_RG_SRCS ${CANTATA_RG_SRCS} main.cpp replaygain.cpp trackscanner.cpp jobcontroller.cpp ../support/thread.cpp)

    if (WIN32)
        ADD_EXECUTABLE(cantata-replaygain WIN32 ${CANTATA_RG_SRCS} ${CANTATA_PO})
        install(TARGETS cantata-replaygain)
    elseif (APPLE)
        ADD_EXECUTABLE(cantata-replaygain ${CANTATA_RG_SRCS} ${CANTATA_PO})
        install(TARGETS cantata-replaygain DESTINATION ${APP_MACOS_DIR})
    else ()
        ADD_EXECUTABLE(cantata-replaygain ${CANTATA_RG_SRCS})
        install(TARGETS cantata-replaygain RUNTIME DESTINATION ${LINUX_LIB_DIR}/cantata)
    endif ()

    find_package(EBUR128)

    if (EBUR128_FOUND)
        message("-- Using system libebur128")
        target_link_libraries(cantata-replaygain EBUR128::EBUR128)
    else ()
        message("-- Using supplied libebur128")
        add_subdirectory(${CMAKE_SOURCE_DIR}/3rdparty/ebur128 ${CMAKE_BINARY_DIR}/3rdparty/ebur128)
        target_link_libraries(cantata-replaygain ebur128)
    endif ()

    if (FFMPEG_FOUND)
        target_link_libraries(cantata-replaygain FFMPEG::avutil FFMPEG::avcodec FFMPEG::avformat)
    endif ()
    if (MPG123_FOUND)
        target_link_libraries(cantata-replaygain ${MPG123_LIBRARIES})
    endif ()
    target_link_libraries(cantata-replaygain Qt${QT_VERSION_MAJOR}::Core Qt${QT_VERSION_MAJOR}::Gui ZLIB::ZLIB)
    if (UNIX AND NOT APPLE)
        target_link_libraries(cantata-replaygain -lpthread)
    endif ()
endif ()

